plugins {
    id 'checkstyle'
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.diffplug.spotless' version '6.1.2'
    id 'com.google.protobuf' version '0.8.18'
    id 'org.sonarqube' version '3.3'
}

apply from: 'dependencies.gradle'
apply from: 'gradle/quality.gradle'
apply from: 'gradle/protobuf.gradle'
apply from: 'gradle/pyatlas.gradle'
apply from: 'gradle/deployment.gradle'

description = "Atlas Library"

sourceCompatibility=11
targetCompatibility=11

repositories
{
    // For geotools
    maven {
      url "https://repo.osgeo.org/repository/release/"
      content {
        // osgeo removed the jar and added a -norce version
        excludeVersion("log4j", "log4j", "1.2.17")
      }
    }
    mavenCentral()
}

configurations
{
    shaded.extendsFrom(implementation)
}

dependencies
{
    implementation packages.artifact
    implementation packages.checkstyle
    api            packages.classgraph
    implementation packages.commons.cli
    implementation packages.commons.compress
    implementation packages.commons.csv
    implementation packages.commons.io
    implementation packages.commons.lang
    implementation packages.commons.math
    implementation packages.commons.text
    implementation packages.diff_utils
    implementation packages.geotools
    api            packages.groovy
    implementation packages.groovy_json
    implementation packages.gson
    api            packages.guava
    api            packages.http
    api            packages.jackson.core
    api            packages.jackson.databind
    api            packages.jackson.dataformat
    api            packages.jim_fs
    implementation packages.jsonassert
    api            packages.jts
    // Support JUnit 3/4 tests
    api            packages.junit.junit4 // only API level due to CoreTestRule
    implementation packages.opencsv
    api            packages.osmosis.core
    implementation packages.osmosis.hstore
    implementation packages.osmosis.osmbinary
    implementation packages.osmosis.pbf
    implementation packages.osmosis.xml
    implementation packages.protobuf_java
    implementation packages.protoc
    implementation packages.slf4j.api
    implementation packages.spatial4j

    testImplementation packages.checkstyle_tests
    // Google Truth is needed for checkstyle tests (as of checkstyle 9.2.1)
    testImplementation packages.google_truth
    testImplementation packages.junit.api
    testImplementation packages.junit.engine
    testImplementation packages.junit.junit4
    testImplementation packages.junit.params
    testImplementation packages.junit.vintage

    checkstyle files("build/libs/${project.name}-${project.version}.jar")
    checkstyle packages.checkstyle

    shaded packages.log4j.api
    shaded packages.log4j.slf4j

}

task shaded(type: Jar) {
    archiveBaseName = project.name
    classifier = 'shaded'
    from
    {
        configurations.shaded.collect
        {
            it.isDirectory() ? it : zipTree(it).matching{
                exclude
                {
                    it.path.contains('META-INF') && (it.path.endsWith('.SF') || it.path.endsWith('.DSA') || it.path.endsWith('.RSA'))
                }
            }
        }
    }
    with jar
    zip64 = true
}

/**
 * Artifact related items
 */
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts
{
    archives javadocJar, sourcesJar
}

/*
 * This is to skip the tasks for which there is a skip<TaskName>=true
 * environment variable
 */
def skippedTaskNames = System.getenv().findAll { key, value ->
    key.startsWith("skip") && value.equalsIgnoreCase("true")
}.keySet().collect { it.substring(4) }
gradle.startParameter.excludedTaskNames += skippedTaskNames

idea {
    project {
        languageLevel = '1.8'
    }
}

/*
 * Workaround Gradle not liking duplicate files
 * I suspect that it is due to fake duplicates.
 * For more information, see https://github.com/gradle/gradle/issues/17236
 * (AKA, remove this when that issue is fixed)
 *
 * Note: It may be useful to disable this from time to time on Gradle update
 *       to see if either it is fixed or if there is better debugging
 *       information available.
 */
tasks.each {
  task -> if (task.hasProperty("duplicatesStrategy")) {
    task.setProperty("duplicatesStrategy", "EXCLUDE")
  }
}
